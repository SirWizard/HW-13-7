// {{ Кауп А.В. Общий модуль программного наполнения форм документов. 14.10.24 
#Область ПрограммныйИнтерфейс
&НаСервере
Процедура ДополнитьФорму (Форма) Экспорт 
// Процедура дополняет формы документов ЗаказПокупателя, Поступление и реализация товаров и услуг, 
// оплата от покупателя и поставщику элементом формы контактное лицо (реквизит объекта Д_КонтактноеЛицо).
//Элемент добавляется под элементом договор
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво (Форма); 
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво (Форма); 
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво (Форма); 
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоПослеДоговора (Форма);
    ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоПослеДоговора (Форма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныйПрограммныйИнтерфейс
&НаСервере
Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво (Форма)
// Процедура выводит на форму документов ПоступлениеТоваровУслуг, РеализацияТоваровУслуг реквизит Контактное лицо. 
// Для Документа ЗаказПокупателя дополнительно выводится в форму ревизит согласованная скидка и команда пересчета сумм таблиц	
	Если Форма.ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		Группа = Форма.Элементы.Добавить ("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
		Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		Группа.Заголовок = "Скидка";
		Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
		Группа.ОтображатьЗаголовок = ЛОЖЬ; 
		Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		 
		ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка); 
		ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВвода.ПутьКДанным = "Объект.Д_СогласованнаяСкидка";
		ПолеВвода.УстановитьДействие ("ПриИзменении", "СогласованнаяСкидкаПриИзменении");
		
		НоваяКоманда = Форма.Команды.Добавить("ПересчитатьТаблицу");
		НоваяКоманда.Заголовок = "Пересчитать таблицу";
		НоваяКоманда.Картинка = БиблиотекаКартинок.Обновить;
		НоваяКоманда.Отображение = ОтображениеКнопки.КартинкаИТекст;
		НоваяКоманда.Действие = "Д_ПересчитатьТаблицу"; 
		
		КнопкаФормы = Форма.Элементы.Добавить("ПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
		КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";

	КонецЕсли;
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво); 
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПолеКонтактноеЛицоПослеДоговора (Форма)
	
	Если Форма.ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.Основание);
	Иначе
		ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.СуммаДокумента);
	КонецЕсли;
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	
КонецПроцедуры

#КонецОбласти
// }}